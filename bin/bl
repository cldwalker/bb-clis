#!/usr/bin/env bb
;; vim: set filetype=clojure:
;; (b)abashka (l)aunch - Launches a bb command quickly by matching on start
;; of command after `bb-`.
;; bb commands assumed to start with `bb-`.
;; Example: `bl t -h` -> bb-table -h

(ns bq
  (:require [clojure.java.io :as io]
            [clojure.string :as str]))

(defn- cmd-exec
  "Hands over current process to new command. Similar to exec in shell script"
  [cmd-and-args]
  (let [pb (doto (ProcessBuilder. cmd-and-args)
                 (.inheritIO))
        p (.start pb)]
    (System/exit (.waitFor p))))

(let [bb-commands (->> (str/split (System/getenv "PATH") #":")
                       (mapcat (fn [path] (seq (.list (io/file path)))))
                       (filter #(str/starts-with? % "bb-")))
      cmd-alias (first *command-line-args*)
      cmds (filterv #(str/starts-with? % (str "bb-" cmd-alias))
                    bb-commands)
      help-string (str "Available commands: " (str/join ", " bb-commands))]
  (cond
    (nil? cmd-alias)
    (println help-string)

    (zero? (count cmds))
    (do
      (println (str "Error: No commands matched for " (pr-str cmd-alias)
                    "\n" help-string))
      (System/exit 1))

    (> (count cmds) 1)
    (do
      (println (str "Error: More than one command matched for " (pr-str cmd-alias)
                    "\n" help-string))
      (System/exit 1))

    :else (cmd-exec (into cmds (rest *command-line-args*)))))
