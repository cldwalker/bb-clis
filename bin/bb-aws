#!/usr/bin/env bb
;; vim: set filetype=clojure:
;; Usage
;; =====
;; Run aws commands the same way the awscli does e.g.
;; - bb-aws s3api list-buckets
;; - bb-aws s3api get-bucket-acl --bucket X
;; Note: Does not support commands with non-option arguments e.g.
;;   bb-aws lambda invoke --function-name FN OUT_FILE
;; Drop into a repl with command's result stored in #'result
;; - bb-aws --repl s3api list-buckets

(ns bb-aws
  {:clj-kondo/config
  '{:linters {:inline-def {:level :off}}}}
  (:require [babashka.pods :as pods]
            [cldwalker.babashka.util.associative :as a]
            [cldwalker.babashka.util :as util]
            [clojure.string :as str]
            [clojure.main :as main]
            [clojure.pprint :as pprint]))

(pods/load-pod 'org.babashka/aws "0.0.6")
(require '[pod.babashka.aws :as aws])

(defn- clojure-case->camel-case
  [s]
  (->> (str/split s #"-")
       (map str/capitalize)
       str/join))

(def aws-command->aws-api-api
  {:s3api :s3})

(defn- parse-options-and-args [all-args]
  (loop [options {}
         args all-args]
    (if (some-> (first args) (str/starts-with? "--"))
      (recur (assoc options
                    (str/replace-first (first args) #"^--" "") (second args))
        (drop 2 args))
      [options args])))

(defn invoke-aws-operation [[cmd subcmd & args] {:keys [region debug]}]
  (let [api (aws-command->aws-api-api (keyword cmd) (keyword cmd))
        op (keyword (clojure-case->camel-case subcmd))
        client (aws/client {:api api :region region})
        [options _] (parse-options-and-args args)
        request (a/map-keys #(-> %
                                 clojure-case->camel-case
                                 (str/replace-first #"[A-Z]" str/lower-case)
                                 keyword)
                            options)]
    (when debug
      (println "API:" api)
      (println "OP:" op)
      (println "REQUEST:" request))
    (try (aws/invoke client {:op op :request request})
      ;; Noisy failure if the wrong number of args given
      ;; See https://github.com/babashka/pod-babashka-aws/blob/d7c4306bdd74ecde16ee3766825b71facb325588/src/pod/babashka/aws.clj#L120-L123 for noise.
      ;; TODO: Actually catch the noisy failure
      (catch Throwable _
        (println "\nError: Unexpected failure occurred. See error message above.")))))

(def cli-options
  [["-h" "--help"]
   ["-d" "--debug"]
   ["-R" "--repl" "Drop into repl with operation result set to #'result"]
   ["-r" "--region REGION"
    :default-desc "$AWS_REGION or us-east-2"
    :default-fn (fn [_] (or (System/getenv "AWS_REGION") "us-east-2"))]])

(defn -main [{:keys [summary arguments options]}]
  (if (or (:help options) (zero? (count arguments)))
    (util/print-summary " COMMAND SUBCOMMAND [SUBCOMMAND-OPTIONS]" summary)
    (let [result (invoke-aws-operation arguments options)]
      (pprint/pprint result)
      (when (:repl options)
        (def result result)
        (main/repl)))))

(when (= *file* (System/getProperty "babashka.file"))
  (util/run-command -main *command-line-args* cli-options :in-order true))

(comment
 (def s3-client (aws/client {:api :s3 :region "us-east-1"}))
 (aws/doc s3-client :ListBuckets)
 (aws/invoke s3-client {:op :ListBuckets})
 )
