#!/usr/bin/env bb
; vim: set filetype=clojure:
; Moves copied text to page with name extracted from block

(ns bb-logseq-move-to-page
  (:require [cldwalker.babashka.util :as util]
            [clojure.string :as str]
            [babashka.tasks :refer [shell]]))

(defn- parse-block [block]
  (->> block
       str/split-lines
       (map #(str/split % #"::\s*"))
       (remove #(< (count %) 2))
       (into {})))

(defn attrs-to-block [attrs]
  (->> attrs
       (map (fn [[k v]] (str k ":: " v)))
       (str/join "\n")))

(defn- move-to-page [block options]
  (let [block-attrs (parse-block block)
        page-name (block-attrs "name")]
    (when-not page-name
      (println "Error: Name attribute required to move page")
      (System/exit 1))
    (let [file (str (:directory options) "/pages/" page-name ".md")
          block (attrs-to-block (into {} (remove #(= "name" (key %)) block-attrs)))]
      (spit file block)
      (shell {:in (block-attrs "name")} "pbcopy")
      (println "Page" page-name "written successfully."))))

(defn -main [{:keys [options summary]}]
  (if (:help options)
    (util/print-summary "" summary)
    (move-to-page (:out (shell {:out :string} "pbpaste")) options)))

(def options
  [["-d" "--directory LOGSEQ-DIR"
    :default-fn (fn [_] (str (System/getenv "HOME") "/code/priv/logseq-notes"))]])

(when (= *file* (System/getProperty "babashka.file"))
  (util/run-command -main *command-line-args* options))
