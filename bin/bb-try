#!/usr/bin/env bb
;; vim: set filetype=clojure:
;; Try a dependency with bb like lein-try

(deps/add-deps '{:deps {io.github.cldwalker/bb-clis {:git/sha "c5da64153fb29e2f3fa807df4228b6e434f00fcd"}}})
; (deps/add-deps '{:deps {io.github.cldwalker/bb-clis {:local/root "."}}})

(ns bb-try
  (:require [cldwalker.bb-clis.cli :as cli]
            [babashka.tasks :refer [shell]]
            [clojure.java.shell :as shell]))

(def cli-options
  [["-h" "--help"]
   ["-c" "--command COMMAND"]
   ["-v" "--version DEPENDENCY_VERSION"]
   ;; print-command only works for commands without env var e.g. clj
   ["-p" "--print-command"]])

(defn- create-deps-string
  [dependency {:keys [version] :or {version "RELEASE"}}]
  (format "{:deps {%s {:mvn/version \"%s\"}}}"
          dependency version))

(defn- exec [args env print-command?]
  ;; print-command doesn't work for bb b/c it can't print env var in a way that's
  ;; reproducible for others
  (if print-command?
    (apply println (map #(if (.contains % " ")
                           (str "'" % "'")
                           %)
                        args))
    (do (apply shell {:extra-env env} args)
      nil)))

(defn- bb-main
  [arguments {:keys [command print-command] :as options}]
  (let [[dependency & bb-args] arguments
        deps-string (create-deps-string dependency options)
        new-classpath (str (System/getenv "BABASHKA_CLASSPATH")
                           ":"
                           (:out (shell {:out :string} "clojure" "-Spath" "-Sdeps" deps-string)))
        bb-invocation (if (-> (shell/sh "which" "rlwrap") :exit zero?)
                        ["rlwrap" command]
                        [command])]
    (exec (concat bb-invocation bb-args)
          {"BABASHKA_CLASSPATH" new-classpath}
          print-command)))

(defn- clj-main [arguments {:keys [print-command] :as options}]
  (let [[dependency & clj-args] arguments
        deps-string (create-deps-string dependency options)
        clj-options ["-Sdeps" deps-string]
        clj-invocation (if (-> (shell/sh "which" "rlwrap") :exit zero?)
                         ["rlwrap" "clojure"]
                         ["clojure"])]
    (exec (concat clj-invocation clj-options clj-args)
          {}
          print-command)))

(defn- -main*
  [arguments {:keys [command] :or {command "bb"} :as options}]
  (if (#{"clj" "clojure"} command)
    (clj-main arguments options)
    (bb-main arguments (assoc options :command command))))

(defn -main
  [{:keys [summary arguments options]}]
  (cond
    (:help options) (cli/print-summary " DEPENDENCY [& ARGS]" summary)
    :else (-main* arguments options)))

(when (= *file* (System/getProperty "babashka.file"))
  (cli/run-command -main *command-line-args* cli-options :in-order true))
