#!/usr/bin/env bb
;; vim: set filetype=clojure:
;; Prints an ascii table for streamed in edn or edn file
;; Edn must be a map or a collection of collections

(deps/add-deps '{:deps {io.github.cldwalker/bb-clis {:git/sha "c5da64153fb29e2f3fa807df4228b6e434f00fcd"}}})
; (deps/add-deps {:deps {'io.github.cldwalker/bb-clis {:local/root (str (fs/parent (fs/parent *file*)))}}})

(ns bb-table
  (:require [clojure.edn :as edn]
            [clojure.string :as str]
            [babashka.deps :as deps]
            [cldwalker.bb-clis.cli :as cli]
            [cldwalker.bb-clis.cli.misc :as misc]))

(defn- get-columns
  "Extract columns from rows. Defaults to searching across all rows"
  [rows]
  (distinct (mapcat identity (map keys rows))))

(defn- selected-columns-int
  [columns columns-filter]
  (let [selected (mapv #(Integer/parseInt %)
                       (str/split columns-filter #"\s*,\s*"))]
    (mapv #(nth columns (dec %)) selected)))

(defn- selected-columns-str
  [columns columns-filter]
  (->> (str/split columns-filter #"\s*,\s*")
       (map #(filter (fn [c] (str/starts-with? (name c) %)) columns))
       (apply concat)))

(defn- selected-columns
  [columns columns-filter]
  (if (re-find #"^\d" columns-filter)
    (selected-columns-int columns columns-filter)
    (selected-columns-str columns columns-filter)))

(defn- print-table
  [columns rows style]
  (deps/add-deps '{:deps {table/table {:mvn/version "0.5.0"}}})
  ((requiring-resolve 'table.core/table) rows :fields columns :style style))

(defn- print-rows* [rows {:keys [columns-filter style number-rows]}]
  (if (and (coll? rows) (-> rows first map?))
    (let [columns (get-columns rows)
          columns_ (if number-rows
                     (into [:number] (remove #(= % :number) columns))
                     columns)
          columns__ (if columns-filter
                      (selected-columns columns_ columns-filter)
                      columns_)]
      (print-table columns__ rows style))
    (cli/error "Input is not an EDN collection of maps")))

(defn- get-sort-key [rows query]
  (let [columns (get-columns rows)]
    (or (first (selected-columns-str columns query))
        ;; Since no matches, auto-try keyword
        (first (selected-columns-str columns (keyword query)))
        query)))

(defn- possibly-sort-rows [rows sort reverse-sort]
  (cond
    sort
    (let [sort-key (get-sort-key rows sort)]
      (sort-by #(get % sort-key) rows))

    reverse-sort
    (let [sort-key (get-sort-key rows reverse-sort)]
      (sort-by #(get % sort-key)
               ;; Handles int and string
               #(compare %2 %1)
               rows))

    :else rows))

(defn- process-rows
  "Currently processes all row values even when we know certain columns are
  selected. Future improvement would be to only operate on selected column
  values"
  [rows {:keys [sort reverse-sort number-rows]}]
  (let [rows_
        (if number-rows
          (mapv #(assoc %1 :number %2)
                rows
                (range 1 (inc (count rows))))
          rows)]
    (possibly-sort-rows rows_ sort reverse-sort)))

(defn- header-counts [rows]
  (let [columns (get-columns rows)
        column-counts (->> rows
                           (map #(zipmap (keys %) (repeat 1)))
                           (apply merge-with +))]
    (mapv (fn [col]
            {:column col :count (column-counts col 0)})
          columns)))

(defn print-rows
  "Prints rows in a table, handling a couple different data structures as input"
  [arg {:keys [print-headers] :as options}]
  (let [arg_ (if print-headers
               (header-counts arg)
               arg)
        rows (cond
               (map? arg_) (map (fn [[k v]] {"key" k "value" v}) arg_)

               ((some-fn vector? list? set?) (first arg_))
               (map (fn [coll] (zipmap (range) coll)) arg_)

               :else arg_)
        processed-rows (process-rows rows options)]
    (print-rows* processed-rows options)))

(defn -main [{:keys [summary _arguments options]}]
  (cond
    (:help options) (cli/print-summary "\nReads from stdin if no arguments given" summary)
    (:file options) (print-rows (-> (:file options) slurp edn/read-string)
                                options)
    :else           (print-rows (first (misc/read-stdin-edn))
                                options)))

(def table-styles
  "Table styles that can be passed to table library via :style"
  #{:plain :org :unicode :github-markdown})

(defn- unalias-choice [choices query]
  (first
   (filter #(str/starts-with? (name %) query)
           choices)))

(def cli-options
  [["-h" "--help"]
   ["-f" "--file FILE"]
   ["-c" "--columns-filter FILTER" "Select columns by comma delimited substring matches or numbers from -H"]
   ["-H" "--print-headers" "Print column headers and their counts across rows"]
   ["-n" "--number-rows"]
   ;; Sort and reverse sort don't handle keywords yet
   ["-s" "--sort COLUMN" "Sort by column"]
   ["-r" "--reverse-sort COLUMN" "Reverse sort by column"]
   ["-S" "--style STYLE" "Table style when using table clojar. Available styles are :plain, :org, :unicode and :github-markdown. Default is :plain."
    :default :plain
    :parse-fn #(keyword (unalias-choice table-styles %))
    :validate [table-styles]]])

(cli/run-command -main *command-line-args* cli-options)
