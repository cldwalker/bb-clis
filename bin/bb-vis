#!/usr/bin/env bb
;; vim: set filetype=clojure:
;; Generates a png, pdf or svg visualization from a vega-lite spec.
;; Spec file can be json or edn.
;; See create-visualization for credits to oz

(ns bb-vis
  (:require [cldwalker.bb-clis.cli :as cli]
            [cldwalker.bb-clis.cli.misc :as misc]
            [clojure.string :as str]
            [clojure.edn :as edn]
            [cheshire.core :as json])
  (:import (java.io File)
           (java.util UUID)))

(defn- tmp-filename
  [ext]
  (str (File/createTempFile (str (UUID/randomUUID)) (str "." (name ext)))))

(defn- json-spec-file
  "Given a vega-lite spec json file, this automatically allows examples
from https://vega.github.io/vega-lite/examples/ to just work. It does this
by detecting if a data url starts with 'data/' and then fully expanding the url."
  [input-file format]
  (let [input-body (try
                     (json/parse-string (slurp input-file))
                     (catch Exception e
                       (cli/error "Unexpected failure while parsing json file:\n" (str e))))
        url (get-in input-body ["data" "url"])]
    (if (and url (str/starts-with? url "data/"))
      (doto (tmp-filename format)
            (spit (json/generate-string
                   (assoc-in input-body
                             ["data" "url"]
                             (str "https://raw.githubusercontent.com/vega/vega/master/docs/"
                                  url)))))
      input-file)))

(defn- input-file-from-stdin
  [format]
  (let [body (slurp System/in)]
    (doto (tmp-filename format)
          (spit body))))

(defn- get-output-file [input-file {:keys [output-file format]}]
  (or output-file
      (str (or (some-> input-file
                       (str/replace-first #"\.\w+$" ""))
               "output")
           "." format)))

(defn- create-visualization
  "Parts of this were taken from oz's vega-cli -
https://github.com/metasoarous/oz/blob/a7881779d11a9b3ed1893a023aed6aed28f65592/src/clj/oz/core.clj#L366"
  [[input-file] {:keys [open format] :as opts}]
  (let [output-file (get-output-file input-file opts)
        input-file_ (if (nil? input-file) (input-file-from-stdin format) input-file)
        spec-file (if (str/ends-with? input-file_ ".edn")
                    (doto (tmp-filename format)
                          (spit (json/generate-string (edn/read-string (slurp input-file_)))))
                    (json-spec-file input-file_ format))]

    (misc/sh (str "vl2" format) spec-file output-file
             ;; Have to override this b/c commands like vl2png don't exit
             ;; properly when they fail
             {:is-error-fn #(or (not= 0 (:exit %))
                                (str/includes? (:err %) "Error"))})
    (when open
      (misc/sh "open" output-file))
    {:input-file spec-file :output-file output-file}))

(defn -main [{:keys [summary arguments options]}]
  (cond
    (or (:help options) (> (count arguments) 1))
    (cli/print-summary " FILE\nReads json from stdin if no file given." summary)

    :else (create-visualization arguments options)))

(def cli-options
  [["-h" "--help"]
   ["-o" "--open"]
   ["-F" "--format FORMAT"
    :validate [#{"png" "pdf" "svg"} "Must be png, pdf or svg"]
    :default "png"]
   ["-f" "--output-file OUTPUT_FILE"
    :default-desc "Defaults to FILE.FORMAT"]])

(cli/run-command -main *command-line-args* cli-options)
