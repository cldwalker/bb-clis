#!/usr/bin/env bb
;; vim: set filetype=clojure:
;; Prints an ascii table for streamed in edn or edn file
;; Edn must be a collection of maps or a map

(ns clj-table
  (:require [clojure.pprint :as pprint]
            [clojure.edn :as edn]
            [cldwalker.babashka.util :as util]))

(defn- print-rows* [rows]
  (if (and (coll? rows) (-> rows first map?))
    (let [fields (distinct (mapcat identity (map keys rows)))]
      (pprint/print-table fields rows))
    (do
      (println "Error: Input is not an EDN collection of maps")
      (System/exit 1))))

(defn print-rows [arg]
  (let [rows (if (map? arg)
               (map (fn [[k v]] {"key" k "value" v}) arg)
               arg)]
    (print-rows* rows)))

(defn -main [{:keys [summary arguments options]}]
  (cond
    (:help options) (util/print-summary "\nReads from stdin if no arguments given" summary)
    (:file options) (print-rows (-> (:file options) slurp edn/read-string))
    :else           (print-rows (first (util/read-stdin-edn)))))

(def cli-options
  [["-h" "--help"]
   ["-f" "--file FILE"]])

(util/run-command -main *command-line-args* cli-options)
